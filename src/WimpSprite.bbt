REM >BASIC:WimpSprite
REM
REM Copyright 1999-2014, Stephen Fryatt (info@stevefryatt.org.uk)
REM
REM This file is part of WimpLib:
REM
REM   http://www.stevefryatt.org.uk/software/
REM
REM Licensed under the EUPL, Version 1.1 only (the "Licence");
REM You may not use this work except in compliance with the
REM Licence.
REM
REM You may obtain a copy of the Licence at:
REM
REM   http://joinup.ec.europa.eu/software/page/eupl
REM
REM Unless required by applicable law or agreed to in
REM writing, software distributed under the Licence is
REM distributed on an "AS IS" basis, WITHOUT WARRANTIES
REM OR CONDITIONS OF ANY KIND, either express or implied.
REM
REM See the Licence for the specific language governing
REM permissions and limitations under the Licence.
REM
REM
REM Wimp Srpite handling library
REM
REM Version 0.01 - 28 February 1999




DEF FNload_user_sprite_area(file_name$)
:
REM Version 0.01
REM
REM Loads a sprite file into a new area of memory.
REM
REM Use is made of the standard Wimp suffixes (22, 23, 24...) in an attempt to
REM get the correct resoultion file.
:
LOCAL area%, size%, type%
:
SYS "OS_File",17,file_name$+FNwimp_sprite_suffix TO type%,,,,size%
:
IF type%=1 THEN
 file_name$+=FNwimp_sprite_suffix
ELSE
 SYS "OS_File",17,file_name$ TO type%,,,,size%
ENDIF
:
IF type%=1 THEN
 size%+=4
 DIM area% size%
 !area%=size%
 area%!8=16
 SYS "OS_SpriteOp",&10A,area%,file_name$
ELSE
 area%=0
ENDIF
:
=area%




DEF FNwimp_sprite_suffix
:
REM Version 0.01
REM
REM Returns the correct suffix for sprite files in the current mode.
REM
REM This does the job of Wimp_ReadSysInfo 2 but gets it correct.
:
LOCAL suffix$, mode_flags%, y_eig%
:
suffix$=""
:
SYS "OS_ReadModeVariable",-1,0 TO ,,mode_flags%
IF mode_flags% AND &10 THEN
 suffix$="23"
ELSE
 SYS "OS_ReadModeVariable",-1,5 TO ,,y_eig%
 IF y_eig%<2 THEN suffix$="22"
ENDIF
:
=suffix$
